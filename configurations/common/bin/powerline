#!/usr/bin/env perl

use strict;
use warnings;
use Cwd;
use Sys::Hostname;
use Term::ANSIColor;

# symbols
my $lambda = "\N{U+03BB}";
my $sep = "\N{U+E0B0}";
my $sep_thin = "\N{U+E0B1}";
my $git_symbol = "\N{U+E0A0}";

# variables
my $dir = getcwd();
my $home = $ENV{'HOME'};
my $host = hostname();

# git related
my $git='';
if(-d "$dir/.git"){
    $git = `git rev-parse --abbrev-ref HEAD`;
    chomp $git;
}

# prettify path
$dir =~ s/^\Q$home\E/~/;
$dir =~ s/^\///;
$dir =~ s/\//$sep_thin /g;
$dir =~ s/^([^~])/\/$1/;
$dir =~ s/^$/\//;

binmode(STDOUT, ":utf8");

my $main_color = "rgb544";
my $alt_color = "rgb445";


print colored($host,"black on_$main_color"),
      colored($sep,"$main_color on_$alt_color"),
      colored($dir,"black on_$alt_color");
if($git){
      print colored($sep,"$alt_color on_$main_color"),
            colored("$git_symbol$git","black on_$main_color"),
            colored($sep,"$main_color on_black"),"\n";
}else{
      print colored($sep,"$alt_color on_black"),"\n";
}
print colored(" $lambda ","black on_$alt_color"),
      colored($sep,"$alt_color on_black"),"\n";
